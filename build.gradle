import java.text.SimpleDateFormat


plugins {
    id 'java'
    id 'groovy'
    id 'java-library'
    id 'maven-publish'
    id 'signing'
    id "io.github.gradle-nexus.publish-plugin" version "1.0.0"
}

def getDevelopmentVersion() {
    def output = new StringBuilder()
    def error = new StringBuilder()
    def gitShortHash = "git -C ${projectDir} rev-parse --short HEAD".execute()
    gitShortHash.waitForProcessOutput(output, error)
    def gitHash = output.toString().trim()
    if (gitHash.isEmpty()) {
        println "git hash is empty: error: ${error.toString()}"
        throw new IllegalStateException("git hash could not be determined")
    }
    "0.0.0-" + new SimpleDateFormat('yyyy-MM-dd\'T\'HH-mm-ss').format(new Date()) + "-" + gitHash
}

def releaseVersion = System.env.RELEASE_VERSION
version = releaseVersion ? releaseVersion : getDevelopmentVersion()
println "Building version = " + version
group = 'com.graphql-java'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(11)
    }
}

repositories {
    mavenCentral()
    mavenLocal()
}

dependencies {
    api "com.graphql-java:graphql-java:22.0"
    api "com.graphql-java:graphql-java-extended-scalars:21.0"
    api "org.hibernate.validator:hibernate-validator:7.0.1.Final"
    api "org.glassfish:jakarta.el:4.0.2"

    testImplementation 'org.slf4j:slf4j-simple:1.7.31'
    testImplementation 'org.spockframework:spock-core:1.3-groovy-2.5'
    testImplementation 'org.codehaus.groovy:groovy-all:2.5.14'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier.set("sources")
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier.set("javadoc")
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

test {
    testLogging {
        exceptionFormat = 'full'
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            groupId group
            artifactId 'graphql-java-extended-validation'
            version version

            artifact sourcesJar
            artifact javadocJar

            pom.withXml {
                asNode().children().last() + {
                    resolveStrategy = Closure.DELEGATE_FIRST
                    name 'graphql-java-extended-validation'
                    description 'A library of extended validation for graphql-java'
                    url 'https://github.com/graphql-java/graphql-java-extended-validation'
                    inceptionYear '2019'

                    scm {
                        url 'https://github.com/graphql-java/graphql-java-extended-validation'
                        connection 'scm:git@github.com:graphql-java/graphql-java-extended-validation.git'
                        developerConnection 'scm:git@github.com:graphql-java/graphql-java-extended-validation.git'
                    }

                    licenses {
                        license {
                            name 'MIT'
                            url 'https://github.com/graphql-java/graphql-java/blob/master/LICENSE.md'
                            distribution 'repo'
                        }
                    }

                    developers {
                        developer {
                            id 'bbakerman'
                            name 'Brad Baker'
                            email 'bbakerman@gmail.com'
                        }
                    }
                }
            }
        }
    }
}

nexusPublishing {
    repositories {
        sonatype {
            username = System.env.MAVEN_CENTRAL_USER
            password = System.env.MAVEN_CENTRAL_PASSWORD
        }
    }
}

signing {
    def signingKey = System.env.MAVEN_CENTRAL_PGP_KEY
    useInMemoryPgpKeys(signingKey, "")
    sign publishing.publications
}

// all publish tasks depend on the build task
tasks.withType(PublishToMavenRepository) {
    dependsOn build
}
