import java.text.SimpleDateFormat


plugins {
    id 'java'
    id 'java-library'
    id 'maven'
    id 'maven-publish'
    id 'signing'
    id "io.github.gradle-nexus.publish-plugin" version "1.0.0"
}

def getDevelopmentVersion() {
    def output = new StringBuilder()
    def error = new StringBuilder()
    def gitShortHash = "git -C ${projectDir} rev-parse --short HEAD".execute()
    gitShortHash.waitForProcessOutput(output, error)
    def gitHash = output.toString().trim()
    if (gitHash.isEmpty()) {
        println "git hash is empty: error: ${error.toString()}"
        throw new IllegalStateException("git hash could not be determined")
    }
    new SimpleDateFormat('yyyy-MM-dd\'T\'HH-mm-ss').format(new Date()) + "-" + gitHash
}


def releaseVersion = System.properties.RELEASE_VERSION
version = releaseVersion ? releaseVersion : getDevelopmentVersion()
println "Building version = " + version
group = 'com.graphql-java'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
    mavenLocal()
}


dependencies {
    compile "com.graphql-java:graphql-java:16.1"
    compile 'org.slf4j:slf4j-api:1.7.30'
    compile "jakarta.validation:jakarta.validation-api:2.0.2"
    compile "org.hibernate.validator:hibernate-validator:6.1.6.Final"
    compile "jakarta.el:jakarta.el-api:3.0.3"
    compile "org.glassfish:jakarta.el:3.0.3"

    testCompile 'org.slf4j:slf4j-simple:1.7.30'
    testCompile 'org.spockframework:spock-core:1.3-groovy-2.5'
    testCompile 'org.codehaus.groovy:groovy-all:2.5.11'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier.set("sources")
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier.set("javadoc")
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

test {
    testLogging {
        exceptionFormat = 'full'
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            groupId group
            artifactId 'graphql-java-extended-validation'
            version version

            artifact sourcesJar
            artifact javadocJar

            pom.withXml {
                asNode().children().last() + {
                    resolveStrategy = Closure.DELEGATE_FIRST
                    name 'graphql-java-extended-validation'
                    description 'A library fo extended validation for graphql-java'
                    url 'https://github.com/graphql-java/graphql-java-extended-validation'
                    inceptionYear '2019'

                    scm {
                        url 'https://github.com/graphql-java/graphql-java-extended-validation'
                        connection 'scm:git@github.com:graphql-java/graphql-java-extended-validation.git'
                        developerConnection 'scm:git@github.com:graphql-java/graphql-java-extended-validation.git'
                    }

                    licenses {
                        license {
                            name 'MIT'
                            url 'https://github.com/graphql-java/graphql-java/blob/master/LICENSE.md'
                            distribution 'repo'
                        }
                    }

                    developers {
                        developer {
                            id 'bbakerman'
                            name 'Brad Baker'
                            email 'bbakerman@gmail.com'
                        }
                    }
                }
            }
        }
    }
}

nexusPublishing {
    repositories {
        sonatype {
            username = System.env.MAVEN_CENTRAL_USER
            password = System.env.MAVEN_CENTRAL_PASSWORD
        }
    }
}

signing {
    def signingKey = System.env.MAVEN_CENTRAL_PGP_KEY
    useInMemoryPgpKeys(signingKey, "")
    sign publishing.publications
}


// all publish tasks depend on the build task
tasks.withType(PublishToMavenRepository) {
    dependsOn build
}

